buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath ANDROID_GRADLE_TOOLS
        classpath GRADLE_MAVEN_PLUGIN
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    // GRADLE DEPENDENCIES
    // SUPPORT
    compile ANDROID_SUPPORT_V4
    // APP COMPAT
    compile ANDROID_SUPPORT_APPCOMPAT_V7

    // PROJECT DEPENDENCIES
    compile BALOOMBA_WSVOLLEY
}

android {
    compileSdkVersion Integer.parseInt(ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion = ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(ANDROID_BUILD_TARGET_SDK_VERSION)

        versionName version
        versionCode 1
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

group = 'fr.baloomba'
version = '0.1.2'

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment {
            MavenDeployment deployment -> signing.signPom(deployment)
        }

        repository(url: sonatypeRepo) {
            authentication(
                    userName: sonatypeUsername,
                    password: sonatypePassword
            )
        }

        pom.artifactId = 'ui_lib'

        pom.project {
            name 'ui_lib'
            packaging 'aar'
            description 'UI library to simplify mobile integration'
            url 'https://github.com/baloomba/android_ui_lib'

            scm {
                url 'scm:git@github.com:baloomba/android_ui_lib.git'
                connection 'scm:git@github.com:baloomba/android_ui_lib.git'
                developerConnection 'scm:git@github.com:baloomba/android_ui_lib.git'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }

            developers {
                developer {
                    id 'baloo'
                    name 'Clement BOULY'
                    email 'bouly.clement@gmail.com'
                }
            }
        }
    }
}
